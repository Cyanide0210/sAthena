//===== eAthena Script ======================================= 
//= Brasilis Town
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Brasilis Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version. Transportation and Basic NPCs.
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//============================================================ 

alberta,247,115,3	script	Crewman#bra2	100,{
	mes "[Crewman]";
	mes "Oye, ¿has oído hablar de un lugar llamado Brasilis?";
	mes "Es una ciudad tropical que es caliente como el desiertopero también llueve mucho. Es un lugar muy misterioso.";
	next;
	mes "[Crewman]";
	mes "Recientemente hemos encontrado una ruta de oceánica nueva para llegar allí fácilmente.";
	mes "Sólo son 10.000 zeny para un viaje redondo! Entonces, ¿quieres ir?";
	next;
	switch(select("Llévame a Brasilis:Me quedaré aquí.")) {
	case 1:
		if (Zeny > 9999) {
			mes "[Crewman]";
			mes "Cool~!! Vayamos~!";
			set zeny,zeny-10000;
			close2;
			warp "brasilis",314,60;
			end;
		}
		else {
			mes "[Crewman]";
			mes "Dije 10,000 zeny.";
			close;
		}
	case 2:
		mes "[Crewman]";
		mes "Bueno, si alguna vez estás interesado, házmelo saber y yo te llevare allí.";
		close;
	}
}

brasilis,316,57,3	script	Crewman#bra1	100,{
	mes "[Crewman]";
	mes "Mi barco se va de regreso a Alberta, ¿quieres unirte a nosotros?";
	next;
	switch(select("Volver a Alberta:Todavia no ~.")) {
	case 1:
		mes "[Crewman]";
		mes "Claro que sí extraño mi hogar.";
		close2;
		warp "alberta",244,115;
		end;
	case 2:
		mes "[Crewman]";
		mes "Bueno, haz lo que quieras. Nos veremos cuando volvamos despues.";
		close;
	}
}

brasilis,155,165,3	script	Signpost#bra1	858,{
	mes ":: Art Museum ::";
	close;
}

brasilis,195,231,3	script	Signpost#bra2	858,{
	mes ":: Verass Monument ::";
	close;
}

brasilis,240,247,3	script	Signpost#bra3	858,{
	mes ":: Market ::";
	mes " ";
	mes "- Para su pociones y Armamento -";
	close;
}

brasilis,303,309,3	script	Signpost#bra4	858,{
	mes ":: Jungle Cable ::";
	mes "";
	mes "- No es para los débiles de corazón -";
	close;
}

brasilis,278,137,3	script	Signpost#bra5	858,{
	mes ":: Brasilis Hotel ::";
	close;
}

brasilis,137,77,5	script	Ice-Cream Maker	85,{
	mes "[Ice Cream Maker]";
	mes "Vengan~vengan~";
	mes "El helado es el aperitivo perfecto para un día de calor ~";
	mes "Solo son ^3355FF100 Zeny^000000~";
	mes "Helado ~";
	mes "Consigan su Helado!";
	next;
	switch(select("Dame uno!:Helado?:Cancelar.")) {
	case 1:
		mes "[Ice Cream Maker]";
		mes "Dado que hay tanta gente quiere obtener un helado fresco se puede pedir sólo el 5 a la vez.";
		mes "Entonces cuantos quieres?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[Ice Cream Maker]";
				mes "Ninguno?";
				mes "Bien.. sal de el camino, tengo clientes que servir.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[Ice Cream Maker]";
				mes "Wow.";
				mes "Has ordenado demasiados.";
				mes "Si usted come más de 5 puede que tenga que luchar con un monstruo en su estómago. Cálmado compañero.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[Ice Cream Maker]";
			mes "Daah~! No tienes suficiente dinero.";
			mes "Solo son ^3355FF100 Zeny^000000~ Enserio!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[Ice Cream Maker]";
			mes "Parece que tienes demasiadas cosas.";
			mes "Aligera tu paquete antes de comprar este.";
			close;
		}
		set zeny,zeny-.@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[Ice Cream Maker]";
		mes "El Helado es...";
		mes "Espera, no sabes";
		mes "lo que es Helado ?";
		mes "Qué roca ha";
		mes "estado viviendo bajo?";
		next;
		mes "[Ice Cream Maker]";
		mes "No voy a empezar a pesar de lo raro que suena.";
		mes "Igualmente, consigue tu Helado justo aqui mientras este bueno y frio.";
		close;
	case 3:
		mes "[Ice Cream Maker]";
		mes "No te pierdas la oportunidad de comer la crema de hielo más grande en todo el planeta!";
		close;
	}
}
